import "../main.ostw";
import "MapData.ostw";
import "../OSTWUtils/OnScreenText.del";

globalvar Number initialCredits;
globalvar Number initialTalentTokens;
globalvar Vector menuPosition = ArrayContains(maps, CurrentMap()) ? mapData[IndexOfArrayValue(maps, CurrentMap())][0] : Vector(0, 150, 0);
globalvar Vector menuFacing = ArrayContains(maps, CurrentMap()) ? mapData[IndexOfArrayValue(maps, CurrentMap())][1] : Forward();
globalvar Any backdropEffect;
globalvar Number[] menuIWTs = [];
globalvar Number[] menuIWProgressBars = [];
globalvar Number index;

playervar BuyMenuState menuState;
enum BuyMenuState {
  INACTIVE,
  MAIN_SCREEN,
  CONSUMABLES,
  TALENTS,
  READY
}
playervar Number selectionIndex;
playervar Number credits = initialCredits;
playervar Number talentTokens = initialTalentTokens;

Player[] PowerupPlayersMenu(Boolean additionalCondition = true): FilteredArray(AllPlayers(PowerupTeam), ArrayElement().menuState != BuyMenuState.INACTIVE && additionalCondition);
Number rowY: -1.1;
Number selectBoxScale: 1.5;
Number readyBtnX: 0;
Number btnsY: rowY - 0.6;

rule: "[Buy Menu] Global interface setup"
{
  # Dim background to improve text contrast. This works by placing the camera within the translucent edge of the orb.
  backdropEffect = CreateEffect(
    VisibleTo:            AllPlayers(),
    Type:                 Effect.Orb,
    Color:                Color.Black,
    Position:             menuPosition - 0.32 * menuFacing,
    Radius:               1,
    Reevaluation:         EffectRev.VisibleToPositionAndRadius
  );
  # Inform opposing team that they are lame and need to wait (jk they're awesome)
  CreateInWorldText(
    VisibleTo:            AllPlayers(OppositeTeamOf(PowerupTeam)),
    Header:               "Purchases in progress, please wait...",
    Position:             OnScreenText.WorldRender(menuPosition, menuFacing, 0, 0),
    Scale:                2,
    Reevaluation:         InworldTextRev.VisibleToPositionAndString,
    Clipping:             Clipping.DoNotClip
  );
  ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
}

void CreateSharedShopUI() "[BUY MENU | SUB] Create shared shop UI"
{
  # Main menu stuff
  CreateInWorldText(
    VisibleTo:            PowerupPlayersMenu(ArrayElement().menuState == BuyMenuState.MAIN_SCREEN),
    Header:               <"<0>Consumables <1>", LocalPlayer().selectionIndex ? "" : "▶ ", IconString(Icon.Poison2)>,
    Position:             OnScreenText.WorldRender(menuPosition, menuFacing, -1, 0),
    Scale:                2.5,
    Clipping:             Clipping.DoNotClip,
    Reevaluation:         InworldTextRev.VisibleToPositionAndString,
    TextColor:            Color.Yellow
  );
  ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
  CreateInWorldText(
    VisibleTo:            PowerupPlayersMenu(ArrayElement().menuState == BuyMenuState.MAIN_SCREEN),
    Header:               <"<0>Talents <1>", LocalPlayer().selectionIndex == 1 ? "▶ " : "", AbilityIconString(Hero.Genji, Button.Jump)>,
    Position:             OnScreenText.WorldRender(menuPosition, menuFacing, 1, 0),
    Scale:                2.5,
    Clipping:             Clipping.DoNotClip,
    Reevaluation:         InworldTextRev.VisibleToPositionAndString,
    TextColor:            Color.Purple
  );
  ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
  // Consumables
  for (index = 0; RoundToInteger(CountOf(powerupData) / 2, Rounding.Up); 1) {
    CreateInWorldText(
      VisibleTo:            PowerupPlayersMenu(ArrayElement().menuState == BuyMenuState.CONSUMABLES),
      Header:               <"<1><0>", powerupData[EvaluateOnce(index)][PowerupDataKey.Name], LocalPlayer().selectionIndex == EvaluateOnce(index) ? "▶ " : "">,
      Position:             OnScreenText.WorldRender(menuPosition, menuFacing, -1.4, 0.7 - 0.3 * EvaluateOnce(index)),
      Scale:                1.5,
      Clipping:             Clipping.DoNotClip,
      Reevaluation:         InworldTextRev.VisibleToPositionAndString,
      TextColor:            powerupData[EvaluateOnce(index)][PowerupDataKey.Color]
    );
    ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
    index += RoundToInteger(CountOf(powerupData) / 2, Rounding.Up);
    if (index < CountOf(powerupData)) {
      CreateInWorldText(
        VisibleTo:            PowerupPlayersMenu(ArrayElement().menuState == BuyMenuState.CONSUMABLES),
        Header:               <"<1><0>", powerupData[EvaluateOnce(index)][PowerupDataKey.Name], LocalPlayer().selectionIndex == EvaluateOnce(index) ? "▶ " : "">,
        Position:             OnScreenText.WorldRender(menuPosition, menuFacing, 1.4, 0.7 - 0.3 * EvaluateOnce(index - RoundToInteger(CountOf(powerupData) / 2, Rounding.Up))),
        Scale:                1.5,
        Clipping:             Clipping.DoNotClip,
        Reevaluation:         InworldTextRev.VisibleToPositionAndString,
        TextColor:            powerupData[EvaluateOnce(index)][PowerupDataKey.Color]
      );
      ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
    }
    index -= RoundToInteger(CountOf(powerupData) / 2, Rounding.Up);
  }
  // Talents
  for (index = 0; RoundToInteger(numOfPowerups / 2, Rounding.Up); 1) {
    CreateInWorldText(
      VisibleTo:            PowerupPlayersMenu(ArrayElement().menuState == BuyMenuState.TALENTS),
      Header:               <"<1><0>", talentData[EvaluateOnce(index)].Name, LocalPlayer().selectionIndex == EvaluateOnce(index) ? "▶ " : "">,
      Position:             OnScreenText.WorldRender(menuPosition, menuFacing, -1.4, 0.7 - 0.3 * EvaluateOnce(index)),
      Scale:                1.5,
      Clipping:             Clipping.DoNotClip,
      Reevaluation:         InworldTextRev.VisibleToPositionAndString,
      TextColor:            talentData[EvaluateOnce(index)].Color
    );
    ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
    index += RoundToInteger(numOfPowerups / 2, Rounding.Up);
    CreateInWorldText(
      VisibleTo:            PowerupPlayersMenu(ArrayElement().menuState == BuyMenuState.TALENTS),
      Header:               <"<1><0>", talentData[EvaluateOnce(index)].Name, LocalPlayer().selectionIndex == EvaluateOnce(index) ? "▶ " : "">,
      Position:             OnScreenText.WorldRender(menuPosition, menuFacing, 1.4, 0.7 - 0.3 * EvaluateOnce(index - RoundToInteger(numOfPowerups / 2, Rounding.Up))),
      Scale:                1.5,
      Clipping:             Clipping.DoNotClip,
      Reevaluation:         InworldTextRev.VisibleToPositionAndString,
      TextColor:            talentData[EvaluateOnce(index)].Color
    );
    ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
    index -= RoundToInteger(numOfPowerups / 2, Rounding.Up);
  }
  # help text
  CreateInWorldText(
    VisibleTo:            TeamOf(LocalPlayer()) == PowerupTeam ? LocalPlayer() : null,
    Header:               <"Move to change selection\n        <0> - Select\n        <1> - Back", InputBindingString(Button.PrimaryFire), InputBindingString(Button.SecondaryFire)>,
    Position:             OnScreenText.WorldRender(menuPosition, menuFacing, -1.4, 1),
    Scale:                1.2,
    Clipping:             Clipping.DoNotClip,
    Reevaluation:         InworldTextRev.VisibleToPositionAndString,
    TextColor:            Color.Orange,
    Spectators:           Spectators.VisibleNever
  );
  ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
}

void CreatePersonalShopUI() "[BUY MENU | SUB] Create personal shop UI"
{
  CreateInWorldText(
    VisibleTo:            EventPlayer(),
    Header:               <"Coins: <0>", credits>,
    Position:             OnScreenText.WorldRender(menuPosition, menuFacing, 1.4, 1.1),
    Scale:                2,
    Clipping:             Clipping.DoNotClip,
    Reevaluation:         InworldTextRev.VisibleToPositionAndString,
    TextColor:            Color.Yellow,
  );
  ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
  CreateInWorldText(
    VisibleTo:            EventPlayer(),
    Header:               <"Tokens: <0>", talentTokens>,
    Position:             OnScreenText.WorldRender(menuPosition, menuFacing, 1.4, 0.9),
    Scale:                2,
    Clipping:             Clipping.DoNotClip,
    Reevaluation:         InworldTextRev.VisibleToPositionAndString,
    TextColor:            Color.Purple,
  );
  ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
  CreateInWorldText(
    VisibleTo:      EventPlayer(),
    Header:         "▒▒▒▒▒",
    Position:       OnScreenText.WorldRender(menuPosition, menuFacing, readyBtnX, btnsY),
    Scale:          4,
    TextColor:      EventPlayer().menuState == BuyMenuState.READY ? CustomColor(0, 162, 255, 255) : CustomColor(255, 153, 0, 255),
    Clipping:       Clipping.DoNotClip,
    Reevaluation:   InworldTextRev.VisibleToAndColor,
    Spectators:     Spectators.VisibleNever
  );
  ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
  CreateInWorldText(
    VisibleTo:      EventPlayer(),
    Header:         <"<1><0>", EventPlayer().menuState == BuyMenuState.READY ? "Unready" : "Ready", LocalPlayer().selectionIndex == EvaluateOnce(index) ? "▶ " : "">,
    Position:       OnScreenText.WorldRender(menuPosition, menuFacing, readyBtnX, btnsY - 0.01),
    Scale:          3,
    TextColor:      Color.White,
    Clipping:       Clipping.DoNotClip,
    Reevaluation:   InworldTextRev.VisibleToStringAndColor,
    Spectators:     Spectators.VisibleNever
  );
  ModifyVariable(menuIWTs, Operation.AppendToArray, LastTextID());
}

rule: "[BUY MENU] Control flow"
if (IsWaitingForPlayers())
{
  CreateSharedShopUI();
  WaitUntil(!IsWaitingForPlayers(), 1000000);
  DestroyShop();
}

void DestroyShop() "[BUY MENU | SUB] Destroy shop UI" {
  while (menuIWTs != []) {
    DestroyInWorldText(FirstOf(menuIWTs));
    ModifyVariable(menuIWTs, Operation.RemoveFromArrayByIndex, 0);
  }
  while (menuIWProgressBars != []) {
    DestroyProgressBarInWorldText(FirstOf(menuIWProgressBars));
    ModifyVariable(menuIWProgressBars, Operation.RemoveFromArrayByIndex, 0);
  }
  DestroyEffect(backdropEffect);
}

rule: "[BUY MENU | Main Screen] Left/right cursor movement"
Event.OngoingPlayer
if (TeamOf() == PowerupTeam)
if (menuState == BuyMenuState.MAIN_SCREEN)
if (AbsoluteValue(XOf(ThrottleOf())) > 0.5)
{
  selectionIndex += XOf(ThrottleOf()) > 0 ? -1 : 1;
  selectionIndex = Min(1, Max(0, selectionIndex));
}

rule: "[BUY MENU | Consumables] Left/right cursor movement"
Event.OngoingPlayer
if (TeamOf() == PowerupTeam)
if (menuState == BuyMenuState.CONSUMABLES)
if (AbsoluteValue(XOf(ThrottleOf())) > 0.5)
{
  selectionIndex = (selectionIndex + (XOf(ThrottleOf()) > 0 ? CountOf(powerupData) - 1 : 1) * RoundToInteger(CountOf(powerupData) / 2, Rounding.Up)) % CountOf(powerupData);
}

rule: "[BUY MENU | Consumables] Up/down cursor movement"
Event.OngoingPlayer
if (TeamOf() == PowerupTeam)
if (menuState == BuyMenuState.CONSUMABLES)
if (AbsoluteValue(ZOf(ThrottleOf())) > 0.5)
{
  selectionIndex = selectionIndex + (ZOf(ThrottleOf()) > 0 ? -1 : 1);
  # Wrap-around going up
  if (ZOf(ThrottleOf()) > 0) {
    if (selectionIndex == -1) {
      selectionIndex = RoundToInteger(CountOf(powerupData) / 2) - 1;
    } else if (selectionIndex == RoundToInteger(CountOf(powerupData) / 2) - 1) {
      selectionIndex = CountOf(powerupData) - 1;
    }
  # Wrap-around going down
  } else {
    if (selectionIndex == RoundToInteger(CountOf(powerupData) / 2)) {
      selectionIndex = 0;
    } else if (selectionIndex == CountOf(powerupData)) {
      selectionIndex = CountOf(powerupData) % 2 ? RoundToInteger(CountOf(powerupData) / 2) : 0;
    }
  }
  selectionIndex = Min(CountOf(powerupData) - 1, Max(0, selectionIndex));
}

rule: "[BUY MENU | Talents] Left/right cursor movement"
Event.OngoingPlayer
if (TeamOf() == PowerupTeam)
if (menuState == BuyMenuState.TALENTS)
if (AbsoluteValue(XOf(ThrottleOf())) > 0.5)
{
  // selectionIndex = (selectionIndex + RoundToInteger((XOf(ThrottleOf()) > 0 ? numOfPowerups - 1 : 1) * numOfPowerups / 2, Rounding.Up)) % numOfPowerups;
  # Movement right
  if (XOf(ThrottleOf()) < 0) {
    selectionIndex += RoundToInteger(numOfPowerups / 2, Rounding.Up);
    # Wrapround to left column
    if (selectionIndex >= numOfPowerups) {
      selectionIndex -= numOfPowerups - 1;
    }
  # Movement left
  } else {
    # Wraparound to right column
    if (selectionIndex < 0) {
      selectionIndex += RoundToInteger(numOfPowerups / 2, Rounding.Up);
    } else {
      selectionIndex -= RoundToInteger(numOfPowerups / 2, Rounding.Up);
    }
  }
}

rule: "[BUY MENU | Talents] Up/down cursor movement"
Event.OngoingPlayer
if (TeamOf() == PowerupTeam)
if (menuState == BuyMenuState.TALENTS)
if (AbsoluteValue(ZOf(ThrottleOf())) > 0.5)
{
  selectionIndex = selectionIndex + (ZOf(ThrottleOf()) > 0 ? -1 : 1);
  # Wrap-around going up
  if (ZOf(ThrottleOf()) > 0) {
    if (selectionIndex == -1) {
      selectionIndex = RoundToInteger(numOfPowerups / 2) - 1;
    } else if (selectionIndex == RoundToInteger(numOfPowerups / 2) - 1) {
      selectionIndex = numOfPowerups - 1;
    }
  # Wrap-around going down
  } else {
    if (selectionIndex == RoundToInteger(numOfPowerups / 2)) {
      selectionIndex = 0;
    } else if (selectionIndex == numOfPowerups) {
      selectionIndex = numOfPowerups % 2 ? RoundToInteger(numOfPowerups / 2) : 0;
    }
  }
  selectionIndex = Min(numOfPowerups - 1, Max(0, selectionIndex));
}

rule: "[BUY MENU] Process primary fire"
Event.OngoingPlayer
if (TeamOf() == PowerupTeam)
if (menuState != BuyMenuState.INACTIVE)
if (IsButtonHeld(Button: Button.PrimaryFire))
{
  switch (menuState) {
    case BuyMenuState.MAIN_SCREEN:
      if (selectionIndex) {
        menuState = BuyMenuState.TALENTS;
      } else {
        menuState = BuyMenuState.CONSUMABLES;
      }
      selectionIndex = 0;
      break;
    case BuyMenuState.CONSUMABLES:
      if (credits < powerupData[selectionIndex][PowerupDataKey.Price]) {
        SmallMessage(EventPlayer(), <"Not Enough Coins">);
        PlayEffect(EventPlayer(), PlayEffect.DebuffImpactSound, null, EventPlayer(), 200);
        Wait(0.25);
        return;
      }
      powerupInventory[selectionIndex] += 1;
      credits -= powerupData[selectionIndex][PowerupDataKey.Price];
      PlayEffect(EventPlayer(), PlayEffect.BuffImpactSound, null, EventPlayer(), 200);
      break;
    case BuyMenuState.TALENTS:
      if (talentTokens < 1) {
        SmallMessage(EventPlayer(), "Not Enough Coins");
        PlayEffect(EventPlayer(), PlayEffect.DebuffImpactSound, null, EventPlayer(), 200);
        Wait(0.25);
        return;
      }
      if (RoundToInteger((talentBaseStats[selectionIndex] - 100) / talentData[selectionIndex].BonusAmount) >= talentData[selectionIndex].MaxStacks) {
        SmallMessage(EventPlayer(), "Can't ugprade this talent any more!");
        PlayEffect(EventPlayer(), PlayEffect.DebuffImpactSound, null, EventPlayer(), 200);
        Wait(0.25);
        return;
      }
      talentBaseStats[selectionIndex] += talentData[selectionIndex].BonusAmount;
      switch (selectionIndex) {
        case TalentType.MOVE_SPEED:
          SetMoveSpeed(EventPlayer(), talentBaseStats[selectionIndex]);
          break;
        case TalentType.MAX_HEALTH:
          SetMaxHealth(EventPlayer(), talentBaseStats[selectionIndex]);
          break;
        case TalentType.DAMAGE_DEALT:
          SetDamageDealt(EventPlayer(), talentBaseStats[selectionIndex]);
          break;
        case TalentType.DAMAGE_RECEIVED:
          SetDamageReceived(EventPlayer(), talentBaseStats[selectionIndex]);
          break;
      }
      lastPurchasedTalent = talentData[selectionIndex];
      talentTokens -= 1;
      SmallMessage(EventPlayer(), <"+<1>% <0>", lastPurchasedTalent.Name, lastPurchasedTalent.BonusAmount>);
      PlayEffect(EventPlayer(), PlayEffect.BuffImpactSound, null, EventPlayer(), 200);
  }
}

rule: "[BUY MENU] Return to main screen"
Event.OngoingPlayer
if (TeamOf() == PowerupTeam)
if (menuState > BuyMenuState.MAIN_SCREEN)
if (IsButtonHeld(Button: Button.SecondaryFire))
{
  selectionIndex = menuState - BuyMenuState.MAIN_SCREEN - 1;
  menuState = BuyMenuState.MAIN_SCREEN;
}

rule: "[BUY MENU] Put powerups team in purchase menu when buy timer is active"
Event.OngoingPlayer
if (IsWaitingForPlayers())
{
  while (!HasSpawned()) {
    Wait(0.25);
    ForcePlayerHero(EventPlayer(), Hero.Zenyatta);
  }
  menuState = BuyMenuState.MAIN_SCREEN;
  WaitUntil(!IsWaitingForPlayers(), 1000000);
  # Prevent race condition issues
  MinWait();
  StopForcingHero();
  menuState = BuyMenuState.INACTIVE;
}

rule: "[BUY MENU] Initialize and exit player menu"
Event.OngoingPlayer
if (menuState > BuyMenuState.INACTIVE)
{
  # Disable all HUD elements
  DisableAllHud();
  # Disable all ability buttons
  DisableAllButtons();
  SetInvisible(EventPlayer(), InvisibleTo.All);
  StartCamera(EventPlayer(), menuPosition, menuPosition + menuFacing);
  StartForcingPlayerPosition(EventPlayer(), menuPosition - menuFacing, false);
  # Automatically cleaned up by shop UI destruction
  if (TeamOf() == PowerupTeam) {
    CreatePersonalShopUI();
  }

  WaitUntil(menuState == BuyMenuState.INACTIVE, 1000000);
  Wait(1);

  # Re-enable all HUD
  EnableAllHud();
  # Re-enable all buttons
  EnableAllButtons();
  SetInvisible(EventPlayer(), InvisibleTo.None);
  StopCamera(EventPlayer());
  StopForcingPlayerPosition(EventPlayer());
}

// TODO: Create talents screen

// HELPER FUNCTIONS

void DisableAllHud() {
    DisableGameModeHud();
    DisableGameModeInworldUI();
    DisableHeroHud();
    DisableScoreboard();
}

void EnableAllHud() {
    EnableGameModeHud();
    EnableGameModeInworldUI();
    EnableHeroHud();
    EnableScoreboard();
}

void DisableAllButtons() {
  DisallowButton(EventPlayer(), Button.PrimaryFire);
  DisallowButton(EventPlayer(), Button.SecondaryFire);
  DisallowButton(EventPlayer(), Button.Ability1);
  DisallowButton(EventPlayer(), Button.Ability2);
  DisallowButton(EventPlayer(), Button.Ultimate);
  DisallowButton(EventPlayer(), Button.Jump);
  DisallowButton(EventPlayer(), Button.Crouch);
  DisallowButton(EventPlayer(), Button.Melee);
  DisallowButton(EventPlayer(), Button.Reload);
}

void EnableAllButtons() {
  AllowButton(EventPlayer(), Button.PrimaryFire);
  AllowButton(EventPlayer(), Button.SecondaryFire);
  AllowButton(EventPlayer(), Button.Ability1);
  AllowButton(EventPlayer(), Button.Ability2);
  AllowButton(EventPlayer(), Button.Ultimate);
  AllowButton(EventPlayer(), Button.Jump);
  AllowButton(EventPlayer(), Button.Crouch);
  AllowButton(EventPlayer(), Button.Melee);
  AllowButton(EventPlayer(), Button.Reload);
}

// TODO: Allow players to ready and unready
