import "../main.ostw";
globalvar Player contestingBot = null;

Vector activeObjectivePosition: CurrentGameMode() == GameMode.Escort || (CurrentGameMode() == GameMode.Hybrid && ObjectiveIndex())
  ? PayloadPosition()
  : ObjectivePosition(ObjectiveIndex());

void useContestabot() "[SUB] Contest-a-bot effects (CALL ASYNC)"
{
  # Safety measure to avoid multiple bots
  # Note under normal circumstances this will not fire in time for the main hook to refund the powerup, but that's okay.
  #
  # We're just trying to make sure if this is called synchronously by accident, the right thing happens.
  if (EntityExists(contestingBot)) {
    powerupWasNotConsumed = true;
    return;
  }
  contestingBot = CreateDummyBot(HeroOf(), PowerupTeam, SlotOf() + NumberOfPlayers(TeamOf()), ObjectivePosition(ObjectiveIndex()), null);
  StartForcingPlayerPosition(contestingBot, activeObjectivePosition + Up(), true);
  StartScalingPlayer(contestingBot, 0.05, false);
  SetInvisible(contestingBot, InvisibleTo.All);
  SetStatus(contestingBot, null, Status.PhasedOut, 1000000);
  StartForcingDummyBotName(contestingBot, "Contest-Chan");
  SmallMessage(AllPlayers(), <"<0> is now contesting/progressing the objective!", PowerupTeam>);
  DisableMovementCollisionWithEnvironment(contestingBot, false);
  Wait(WorkshopSettingInteger("Contestabot", "Contest Duration", 15, 1, 60, 0));
  DestroyDummyBot(TeamOf(contestingBot), SlotOf(contestingBot));
  contestingBot = null;
  SmallMessage(AllPlayers(), <"<0> is no longer contesting/progressing the objective", PowerupTeam>);
}
