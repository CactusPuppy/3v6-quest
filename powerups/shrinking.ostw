import "../main.ostw";

globalvar Number shrinkDuration = WorkshopSettingInteger("Shrinking Tablet", "Shrink Duration", 15, 1, 60, 0);
globalvar Number shrunkScale = WorkshopSettingReal("Shrinking Tablet", "Shrunken Scale", 0.25, 0.25, 1, 1);
globalvar Number shrunkVoicelinePitch = WorkshopSettingReal("Shrinking Tablet", "Shrunken Voice Pitch", 1.5, 1, 1.5);

playervar Number shrinkTimer;
playervar Number shrinkScale;
playervar Number shrinkPitch;

void useShrink() "[SUB] Shrinking Tablet effects"
{
  StartScalingPlayer(EventPlayer(), shrinkScale, true);
  StartModifyingHeroVoiceLines(EventPlayer(), shrinkPitch, true);
  ChaseVariableOverTime(shrinkScale, shrunkScale, 0.3, TimeChaseReevaluation.None);
  ChaseVariableOverTime(shrinkPitch, shrunkVoicelinePitch, 0.3, TimeChaseReevaluation.None);
  PlayEffect(AllPlayers(), PlayEffect.MoiraFadeDisappearEffect, <Color>TeamOf(EventPlayer()), EventPlayer(), 1.2);
  PlayEffect(AllPlayers(), PlayEffect.MoiraFadeDisappearSound, null, EventPlayer(), 200);
  Wait(0.3);

  shrinkTimer = shrinkDuration;
  ChaseVariableAtRate(shrinkTimer, 0, 1, RateChaseReevaluation.None);
}

rule: "[SHRINKING] If player dies, cancel shrunken state"
Event.OnDeath
if (shrinkTimer > 0)
{
  shrinkTimer = 0;
}

rule: "[SHRINKING] On timer completion, revert shrunken state"
Event.OngoingPlayer
if (shrinkTimer > 0)
{
  WaitUntil(shrinkTimer == 0, shrinkDuration);

  StopChasingVariable(shrinkTimer);
  ChaseVariableOverTime(shrinkScale, 1, 0.3, TimeChaseReevaluation.None);
  ChaseVariableOverTime(shrinkPitch, 1, 0.3, TimeChaseReevaluation.None);
  PlayEffect(AllPlayers(), PlayEffect.MoiraFadeReappearEffect, <Color>TeamOf(EventPlayer()), EventPlayer(), 1.2);
  PlayEffect(AllPlayers(), PlayEffect.MoiraFadeReappearSound, null, EventPlayer(), 200);
  Wait(0.3);

  StopScalingPlayer(EventPlayer());
  StopModifyingHeroVoiceLines(EventPlayer());
}
